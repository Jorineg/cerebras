// 1600-1658 -> 58. receive: 16014, 1605, 1616, 1616 
// 1658-1713 -> 55. receive: 1672, 1632, 1651, 1647
// 1713-1768 -> 55. receive: 1727, 1688, 1709, 1704
// 1768-1823 -> 55. receive: 1782, 1743, 1764, 1759


const simprint = @import_module("<simprint>");

param memcpy_params: comptime_struct;
param is_static_region: bool;
param steps: i16;

param send_e_col: color;
param send_w_col: color;
param send_n_col: color;
param send_s_col: color;

param recv_e_col: color;
param recv_w_col: color;
param recv_n_col: color;
param recv_s_col: color;

param row: i16;
param col: i16;

param width: i16;
param height: i16;

const receive_n_to_send = if (is_static_region) 1 else 4;

// Queue IDs
const send_color_oq_e = @get_output_queue(2);  // 0 is reserved
const send_color_oq_w = @get_output_queue(1);
const send_color_oq_n = @get_output_queue(3);
const send_color_oq_s = @get_output_queue(4);

const recv_color_iq_e = @get_input_queue(3);  // 0 and 1 are reserved
const recv_color_iq_w = @get_input_queue(2);
const recv_color_iq_n = @get_input_queue(4);
const recv_color_iq_s = @get_input_queue(5);

const is_corner = (col == 0 or col == width-1) and (row == 0 or row == height-1);

const receive_data_task_e_id: data_task_id = if (@is_arch("wse3")) @get_data_task_id(recv_color_iq_e) else @get_data_task_id(recv_e_col);
const receive_data_task_w_id: data_task_id = if (@is_arch("wse3")) @get_data_task_id(recv_color_iq_w) else @get_data_task_id(recv_w_col);
const receive_data_task_n_id: data_task_id = if (@is_arch("wse3")) @get_data_task_id(recv_color_iq_n) else @get_data_task_id(recv_n_col);
const receive_data_task_s_id: data_task_id = if (@is_arch("wse3")) @get_data_task_id(recv_color_iq_s) else @get_data_task_id(recv_s_col);

// Task ID used by a local task to unblock cmd stream
const exit_task_id: local_task_id = @get_local_task_id(9);

// memcpy module provides infrastructure for copying data
// and launching functions from the host
const sys_mod = @import_module("<memcpy/memcpy>", memcpy_params);

const rank: i16 = 1;
var weights = [rank+1]f32 {1.0, 0.5};
const zero: [1]f32 = [1]f32 {0.0};

var matrix: [1]f32 = [1]f32 {0.0};
var matrix_ptr: [*]f32 = &matrix;

var steps_executed: i16 = 0;
var wait_steps: u16 = 0;

var recv_counter: i16 = 0;

// DSDs for accessing weights and matrix
const weights_dsd = @get_dsd(mem1d_dsd, .{ .tensor_access = |i|{rank+1} -> weights[i+1] });
const matrix_dsd = @get_dsd(mem1d_dsd, .{ .tensor_access = |i|{1} -> matrix[0] });
const zero_dsd = @get_dsd(mem1d_dsd, .{ .tensor_access = |i|{1} -> zero[0] });

const send_dsd_e = @get_dsd(fabout_dsd, .{ .fabric_color = send_e_col, .extent = 1, .output_queue = send_color_oq_e});
const send_dsd_w = @get_dsd(fabout_dsd, .{ .fabric_color = send_w_col, .extent = 1, .output_queue = send_color_oq_w});
const send_dsd_n = @get_dsd(fabout_dsd, .{ .fabric_color = send_n_col, .extent = 1, .output_queue = send_color_oq_n});
const send_dsd_s = @get_dsd(fabout_dsd, .{ .fabric_color = send_s_col, .extent = 1, .output_queue = send_color_oq_s});

const recv_dsd_e = @get_dsd(fabin_dsd, .{ .fabric_color = recv_e_col, .extent = 1, .input_queue = recv_color_iq_e});
const recv_dsd_w = @get_dsd(fabin_dsd, .{ .fabric_color = recv_w_col, .extent = 1, .input_queue = recv_color_iq_w});
const recv_dsd_n = @get_dsd(fabin_dsd, .{ .fabric_color = recv_n_col, .extent = 1, .input_queue = recv_color_iq_n});
const recv_dsd_s = @get_dsd(fabin_dsd, .{ .fabric_color = recv_s_col, .extent = 1, .input_queue = recv_color_iq_s});


// function that initializes weights
// divide weights to not have to divide in the loop
// first weight is for center element, others are for neighbors
// so that sum of w[0] + 4*w[1] + 4*w[2] + ... + 4*w[rank] = 1
fn init_weights() void {
  var sum: f32 = 0.0;
  for (@range(i16, rank+1)) |i| {
    sum += if (i == 0) weights[i] else 4.0*weights[i];
  }
  for (@range(i16, rank+1)) |i| {
    weights[i] /= sum;
  }
}


task receive_data_e(data: f32) void {
  receive_data_fn(data);
}

task receive_data_w(data: f32) void {
  receive_data_fn(data);
}

task receive_data_n(data: f32) void {
  receive_data_fn(data);
}

task receive_data_s(data: f32) void {
  receive_data_fn(data);
}


// add received value to result
fn receive_data_fn(data : f32) void {
  if(! is_static_region){
    @fmacs(matrix_dsd, matrix_dsd, weights_dsd, data);
  }

  recv_counter+=1;
  if (recv_counter == receive_n_to_send) {    // 4 for non-static regions, 1 for static regions
    if (steps_executed >= steps) {
      @activate(exit_task_id);
    }else{
      // add own value to result
      send_to_neighbors();
      recv_counter = 0;
      steps_executed+=1;
    }
  }
}



fn send_to_neighbors() void {
  if(col < width-1 and row > 0 and row < height-1) {
    @fmovs(send_dsd_e, matrix_dsd);
  }
  if(col > 0 and row > 0 and row < height-1) {
    @fmovs(send_dsd_w, matrix_dsd);
  }
  if(row < height-1 and col > 0 and col < width-1) {
    @fmovs(send_dsd_s, matrix_dsd);
  }
  if(row > 0 and col > 0 and col < width-1) {
    @fmovs(send_dsd_n, matrix_dsd);
  }

  // add own value to result
  if(! is_static_region){
    @fmuls(matrix_dsd, matrix_dsd, weights[0]);
  }
}

// Call gemv function and send/ receive partial result y
fn compute() void {
  init_weights();
  steps_executed += 1;
  send_to_neighbors();
  if (is_corner) {
    @activate(exit_task_id);
  }
}


task exit_task() void {
  sys_mod.unblock_cmd_stream();
}

comptime {
  // When exit_task_id is activated, exit_task will execute
  @bind_local_task(exit_task, exit_task_id);

  // bind same receive_data task to all receive data task ids
  @bind_data_task(receive_data_e, receive_data_task_e_id);
  @bind_data_task(receive_data_w, receive_data_task_w_id);
  @bind_data_task(receive_data_n, receive_data_task_n_id);
  @bind_data_task(receive_data_s, receive_data_task_s_id);  

  // On WSE-3, we must explicitly initialize input and output queues
  if (@is_arch("wse3")) {
    @initialize_queue(send_color_oq_e, .{ .color = send_color_oq_e });
    @initialize_queue(send_color_oq_w, .{ .color = send_color_oq_w });
    @initialize_queue(send_color_oq_n, .{ .color = send_color_oq_n });
    @initialize_queue(send_color_oq_s, .{ .color = send_color_oq_s });

    @initialize_queue(recv_color_iq_e, .{ .color = recv_color_iq_e });
    @initialize_queue(recv_color_iq_w, .{ .color = recv_color_iq_w });
    @initialize_queue(recv_color_iq_n, .{ .color = recv_color_iq_n });
    @initialize_queue(recv_color_iq_s, .{ .color = recv_color_iq_s });
  }

  @export_symbol(matrix_ptr, "matrix");
  @export_symbol(compute);
}