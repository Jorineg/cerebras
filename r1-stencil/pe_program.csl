// 1605 - 1622 -> 17 cycles
// 1622 - 1638 -> 16 cycles
// 1638 - 1652 -> 14 cycles
// 1652 - 1668 -> 16 cycles
// 1668 - 1682 -> 14 cycles
// 1682 - 1698 -> 16 cycles



// sending data to neighbors: 1 cycle
// update value with own value from previous step: 3 cycle
// for each neighbor do following:
//  - sometimes one wait cycle...
//  - update value with neighbor value (fmacs): 3 cycle




// const simprint = @import_module("<simprint>");
// const tile_config = @import_module("<tile_config>");

param memcpy_params: comptime_struct;
param is_static_region: bool;
param steps: i16;

param send_col: color;

param recv_e_col: color;
param recv_w_col: color;
param recv_n_col: color;
param recv_s_col: color;

param row: i16;
param col: i16;

param width: i16;
param height: i16;

const receive_n_to_send = if (is_static_region) 1 else 4;

// Queue IDs
const send_color_oq = @get_output_queue(1);  // 0 is reserved

const recv_color_iq_e = @get_input_queue(3);  // 0 and 1 are reserved
const recv_color_iq_w = @get_input_queue(2);
const recv_color_iq_n = @get_input_queue(4);
const recv_color_iq_s = @get_input_queue(5);

const is_corner = (col == 0 or col == width-1) and (row == 0 or row == height-1);

// Task ID used by a local task to unblock cmd stream
const exit_task_id: local_task_id = @get_local_task_id(9);

// memcpy module provides infrastructure for copying data
// and launching functions from the host
const sys_mod = @import_module("<memcpy/memcpy>", memcpy_params);

const rank: i16 = 1;
var weights = [rank+1]f32 {1.0, 0.5};
const zero: [1]f32 = [1]f32 {0.0};

const weight_zero: f32 = 1.0/3.0;
const weight_one: f32 = 1.0/6.0;

// var received_data: [4]f32 = [4]f32 {0.0, 0.0, 0.0, 0.0};
var intermediate_sum_1: [1]f32 = [1]f32 {0.0};
var intermediate_sum_2: [1]f32 = [1]f32 {0.0};

var dev_null: [1]f32;

var matrix: [1]f32 = [1]f32 {0.0};
var matrix_ptr: [*]f32 = &matrix;
var pre_processed_matrix: [1]f32 = [1]f32 {0.0};

var steps_executed: i16 = 0;
var wait_steps: u16 = 0;

var recv_counter: i16 = 0;

// DSDs for accessing weights and matrix
const weights_dsd = @get_dsd(mem1d_dsd, .{ .tensor_access = |i|{rank+1} -> weights[i+1] });
const matrix_dsd = @get_dsd(mem1d_dsd, .{ .tensor_access = |i|{1} -> matrix[0] });
const zero_dsd = @get_dsd(mem1d_dsd, .{ .tensor_access = |i|{1} -> zero[0] });
const dev_null_dsd = @get_dsd(mem1d_dsd, .{ .tensor_access = |i|{1} -> dev_null[0] });
const pre_processed_matrix_dsd = @get_dsd(mem1d_dsd, .{ .tensor_access = |i|{1} -> pre_processed_matrix[0] });

const send_dsd = @get_dsd(fabout_dsd, .{ .fabric_color = send_col, .extent = 1, .output_queue = send_color_oq });

const recv_dsd_e = @get_dsd(fabin_dsd, .{ .fabric_color = recv_e_col, .extent = 1, .input_queue = recv_color_iq_e});
const recv_dsd_w = @get_dsd(fabin_dsd, .{ .fabric_color = recv_w_col, .extent = 1, .input_queue = recv_color_iq_w});
const recv_dsd_n = @get_dsd(fabin_dsd, .{ .fabric_color = recv_n_col, .extent = 1, .input_queue = recv_color_iq_n});
const recv_dsd_s = @get_dsd(fabin_dsd, .{ .fabric_color = recv_s_col, .extent = 1, .input_queue = recv_color_iq_s});

// const received_data_dsd = @get_dsd(mem1d_dsd, .{ .tensor_access = |i|{4} -> received_data[i] });
const intermediate_sum_1_dsd = @get_dsd(mem1d_dsd, .{ .tensor_access = |i|{1} -> intermediate_sum_1[0] });
const intermediate_sum_2_dsd = @get_dsd(mem1d_dsd, .{ .tensor_access = |i|{1} -> intermediate_sum_2[0] });

// DSRs
const weights_dsr = @get_dsr(dsr_src1, 12);
const matrix_dsr = @get_dsr(dsr_src0, 1);
// const matrix_dsr_src0_2 = @get_dsr(dsr_src0, 3);
const matrix_dsr_dest = @get_dsr(dsr_dest, 3);
const dev_null_dsr = @get_dsr(dsr_dest, 12);
const pre_processed_matrix_dsr = @get_dsr(dsr_src1, 3);


const send_dsr = @get_dsr(dsr_dest, 1);

const recv_dsr_e = @get_dsr(dsr_src0, 8);
const recv_dsr_w = @get_dsr(dsr_src1, 9);
const recv_dsr_n = @get_dsr(dsr_src0, 10);
const recv_dsr_s = @get_dsr(dsr_src1, 11);

const intermediate_sum_1_dsr_dest = @get_dsr(dsr_dest, 8);
const intermediate_sum_2_dsr_dest = @get_dsr(dsr_dest, 10);
const intermediate_sum_1_dsr_src1 = @get_dsr(dsr_src1, 4);
const intermediate_sum_2_dsr_src1 = @get_dsr(dsr_src1, 5);

// const received_data_dsr_dest = @get_dsr(dsr_dest, 4);
// const received_data_dsr_src0 = @get_dsr(dsr_src0, 2);
// const received_data_dsr_src1 = @get_dsr(dsr_src1, 1);


// function that initializes weights
// divide weights to not have to divide in the loop
// first weight is for center element, others are for neighbors
// so that sum of w[0] + 4*w[1] + 4*w[2] + ... + 4*w[rank] = 1
fn init_weights() void {
  var sum: f32 = 0.0;
  for (@range(i16, rank+1)) |i| {
    sum += if (i == 0) weights[i] else 4.0*weights[i];
  }
  for (@range(i16, rank+1)) |i| {
    weights[i] /= sum;
  }
  pre_processed_matrix[0] = matrix[0] * weights[1];
}

fn loop() void {
  // synchronized version
  for (@range(i16, steps)) |i| {
    send_to_neighbors();
    if (!is_static_region){
    // add own value to result
      @fmuls(matrix_dsr, matrix_dsr, weight_zero);


      // @fadds(matrix_dsr_dest, matrix_dsr_src0_2, recv_dsr_w);
      // @fadds(matrix_dsr_dest, matrix_dsr_src0_2, recv_dsr_e);
      // @fadds(matrix_dsr_dest, matrix_dsr_src0_2, recv_dsr_n);
      // @fadds(matrix_dsr_dest, matrix_dsr_src0_2, recv_dsr_s);

      @fadds(intermediate_sum_1_dsr_dest, recv_dsr_e, recv_dsr_w);
      @fadds(intermediate_sum_2_dsr_dest, recv_dsr_n, recv_dsr_s);
      @fadds(matrix_dsr, matrix_dsr, intermediate_sum_1_dsr_src1);
      @fadds(matrix_dsr, matrix_dsr, intermediate_sum_2_dsr_src1);

      // @fmovs(received_data_dsr_dest, recv_dsr_e);
      // @fmovs(received_data_dsr_dest, recv_dsr_w);
      // @fmovs(received_data_dsr_dest, recv_dsr_n);
      // @fmovs(received_data_dsr_dest, recv_dsr_s);

      // @fadds(matrix_dsr, matrix_dsr, received_data_dsr_src1);
      // @fadds(matrix_dsr, matrix_dsr, received_data_dsr_src1);
      // @fadds(matrix_dsr, matrix_dsr, received_data_dsr_src1);
      // @fadds(matrix_dsr, matrix_dsr, received_data_dsr_src1);

    }else{
      // receive and delete data
      border_receive_data();
    }
  }

  @activate(exit_task_id);
}


fn border_receive_data() void {
  if (col == 0) {
    @fmovs(dev_null_dsd, recv_dsd_e);
  }else if (col == width-1) {
    @fmovs(dev_null_dsd, recv_dsd_w);
  }else if (row == 0) {
    @fmovs(dev_null_dsd, recv_dsd_s);
  }else if (row == height-1) {
    @fmovs(dev_null_dsd, recv_dsd_n);
  }
}



fn send_to_neighbors() void {
  if(is_static_region){
    @fmovs(send_dsr, pre_processed_matrix_dsr);
  }else{
    @fmuls(send_dsr, matrix_dsr, weight_one);
  }
}

// Call gemv function and send/ receive partial result y
fn compute() void {
  init_weights();
  if (is_corner) {
    @activate(exit_task_id);
  }else{
    if(is_static_region){
      send_to_neighbors(); // send once more so that input queues are never empty
    }
    loop();
  }
}


task exit_task() void {
  sys_mod.unblock_cmd_stream();
}

comptime {
  // When exit_task_id is activated, exit_task will execute
  @bind_local_task(exit_task, exit_task_id);

  // load DSDs to DSRs
  @load_to_dsr(weights_dsr, weights_dsd);
  @load_to_dsr(matrix_dsr, matrix_dsd);
  // @load_to_dsr(matrix_dsr_src0_2, matrix_dsd);
  @load_to_dsr(matrix_dsr_dest, matrix_dsd);
  @load_to_dsr(dev_null_dsr, dev_null_dsd);
  @load_to_dsr(pre_processed_matrix_dsr, pre_processed_matrix_dsd);
  
  @load_to_dsr(send_dsr, send_dsd);

  @load_to_dsr(recv_dsr_e, recv_dsd_e);
  @load_to_dsr(recv_dsr_w, recv_dsd_w);
  @load_to_dsr(recv_dsr_n, recv_dsd_n);
  @load_to_dsr(recv_dsr_s, recv_dsd_s);

  // @load_to_dsr(received_data_dsr_dest, received_data_dsd, .{ .save_address = true});
  // @load_to_dsr(received_data_dsr_src0, received_data_dsd, .{ .save_address = true});
  // @load_to_dsr(received_data_dsr_src1, received_data_dsd, .{ .save_address = true});

  @load_to_dsr(intermediate_sum_1_dsr_dest, intermediate_sum_1_dsd);
  @load_to_dsr(intermediate_sum_2_dsr_dest, intermediate_sum_2_dsd);
  @load_to_dsr(intermediate_sum_1_dsr_src1, intermediate_sum_1_dsd);
  @load_to_dsr(intermediate_sum_2_dsr_src1, intermediate_sum_2_dsd);

  // On WSE-3, we must explicitly initialize input and output queues
  if (@is_arch("wse3")) {
    @initialize_queue(send_color_oq, .{ .color = send_col });

    @initialize_queue(recv_color_iq_e, .{ .color = recv_e_col });
    @initialize_queue(recv_color_iq_w, .{ .color = recv_w_col });
    @initialize_queue(recv_color_iq_n, .{ .color = recv_n_col });
    @initialize_queue(recv_color_iq_s, .{ .color = recv_s_col });
  }else{

    // must also be initialized on WSE-2 manually if using dsrs
    // not documented !!!
    @initialize_queue(recv_color_iq_e, .{ .color = recv_e_col });
    @initialize_queue(recv_color_iq_w, .{ .color = recv_w_col });
    @initialize_queue(recv_color_iq_n, .{ .color = recv_n_col });
    @initialize_queue(recv_color_iq_s, .{ .color = recv_s_col });
  }

  @export_symbol(matrix_ptr, "matrix");
  @export_symbol(compute);
}